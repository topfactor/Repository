#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент ИЛИ МобильныйКлиент Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для корректной работы необходим режим тонкого или толстого клиента.'"));
		Отказ = Истина;
		Возврат;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьФайлНастроек(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок      = НСтр("ru = 'Укажите имя файла настроек сравнения/объединения'");
	Диалог.Расширение     = "xml";
	Диалог.ПредварительныйПросмотр = Ложь;
	Диалог.ПолноеИмяФайла = НСтр("ru = 'ФайлНастроекСравнения'");
	Диалог.Фильтр = НСтр("ru = 'Файл настроек сравнения/объединения (*.xml)|*.xml'");
	ОписаниеОповещения = Новый ОписаниеОповещения("СформироватьФайлНастроекПродолжение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаИсправительнуюВерсию(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок      = НСтр("ru = 'Укажите файл новой версии БСП'");
	Диалог.Расширение     = "cf";
	Диалог.ПредварительныйПросмотр = Ложь;
	Диалог.ПолноеИмяФайла = НСтр("ru = '1Cv8'");
	Диалог.Фильтр = НСтр("ru = 'Конфигурация информационной базы 1С:Предприятия 8 (*.cf)|*.cf'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьНаИсправительнуюВерсиюПродолжение", ЭтотОбъект);
	НачатьПомещениеФайлов(ОписаниеОповещения, , Диалог, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьФайлНастроекПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВХранилище = СформироватьФайлНастроекНаСервере();
	ПолноеИмяФайла   = Результат[0];
	ПолучаемыеФайлы = Новый Массив;
	ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ПолноеИмяФайла, ДанныеВХранилище));
	Обработчик = Новый ОписаниеОповещения();
	
	НачатьПолучениеФайлов(Обработчик, ПолучаемыеФайлы, ПолноеИмяФайла, Ложь);
	
КонецПроцедуры

&НаСервере
Функция СформироватьФайлНастроекНаСервере()
	ИмяФайлаВыгрузки = ПолучитьИмяВременногоФайла("xml");
	РеквизитФормыВЗначение("Объект").СформироватьФайлНастроекСравненияОбъединения(ИмяФайлаВыгрузки);
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаВыгрузки);
	УдалитьФайлы(ИмяФайлаВыгрузки);
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ОбновитьНаИсправительнуюВерсиюПродолжение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьНаИсправительнуюВерсиюНаСервере(Результат);
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаИсправительнуюВерсиюНаСервере(ВыбранныйФайлОбновления)
	ИмяФайлаОбновления = ПолучитьИмяВременногоФайла("cf");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ВыбранныйФайлОбновления[0].Хранение);
	ДвоичныеДанные.Записать(ИмяФайлаОбновления);
	
	РеквизитФормыВЗначение("Объект").ОбновитьНаИсправительнуюВерсию(ИмяФайлаОбновления);
	УдалитьФайлы(ИмяФайлаОбновления);
КонецПроцедуры

#КонецОбласти
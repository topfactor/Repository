// См. в модуле объекта раздел "ЗАПОЛНЯЕТСЯ РАЗРАБОТЧИКАМИ ПОДСИСТЕМ".

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИсточникОграничений = "ИзТабличногоДокумента";
	ВыполнятьВыгрузкуВоВременнуюПапку = Истина;
	
	СвойстваВидовПользователей.Добавить();
	СвойстваВидовПользователей.Добавить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Список.ОбновитьТекстРедактирования();
	ОбновитьТекстыОграниченияСписка();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Коллекции = Новый СписокЗначений;
	Коллекции.Добавить("ПланыОбмена");
	Коллекции.Добавить("Справочники");
	Коллекции.Добавить("Документы");
	Коллекции.Добавить("ЖурналыДокументов");
	Коллекции.Добавить("ПланыВидовХарактеристик");
	Коллекции.Добавить("ПланыСчетов");
	Коллекции.Добавить("ПланыВидовРасчета");
	Коллекции.Добавить("РегистрыСведений");
	Коллекции.Добавить("РегистрыНакопления");
	Коллекции.Добавить("РегистрыБухгалтерии");
	Коллекции.Добавить("РегистрыРасчета");
	Коллекции.Добавить("БизнесПроцессы");
	Коллекции.Добавить("Задачи");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборЕдинственного", Истина);
	ПараметрыФормы.Вставить("НачальноеЗначениеВыбора", Список);
	ПараметрыФормы.Вставить("КоллекцииВыбираемыхОбъектовМетаданных", Коллекции);
	
	ОткрытьФорму("ОбщаяФорма.ВыборОбъектовМетаданных", ПараметрыФормы, Элемент,,,, Новый ОписаниеОповещения(
		"СписокНачалоВыбораЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Список = Текст;
	ОбновитьТекстыОграниченияСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстОграниченияИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ПриИзмененииТекстаОграничения(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстОграниченияДляВнешнихПользователейИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ПриИзмененииТекстаОграничения(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникОграниченийПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаГотовойВыгрузкиКонфигурацииВФайлыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Каталог = ПапкаГотовойВыгрузкиКонфигурацииВФайлы;
	Диалог.Заголовок = НСтр("ru = 'Выбор папки, содержащей файлы выгрузки конфигурации'");
	
	Диалог.Показать(Новый ОписаниеОповещения(
		"ПапкаГотовойВыгрузкиКонфигурацииВФайлыНачалоВыбораПослеВыбора", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОписаниеПрофилейВыполнить(Команда)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ОписаниеНачальногоЗаполненияПрофилейНаВстроенномЯзыке(КонкретныйПрофиль));
	ТекстовыйДокумент.Показать(НСтр("ru = 'Описание начального заполнения имеющихся профилей групп доступа'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОграничение(Команда)
	
	ПроверитьУказанноеОграничение(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстДляВставки(Команда)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстДляВставки());
	ТекстовыйДокумент.Показать(НСтр("ru = 'Тексты для вставки в модуль и в роли'"));
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОграничениеВРоли(Команда)
	
	ПроверитьУказанноеОграничение(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнятьВыгрузкуВоВременнуюПапкуПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПолноеОписание(Команда)
	
	СформироватьОписание();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОписаниеРазличий(Команда)
	
	СформироватьОписание(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокНачалоВыбораЗавершение(СписокЗначений, Контекст) Экспорт
	
	Если ТипЗнч(СписокЗначений) <> Тип("СписокЗначений")
	 Или СписокЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Список = СписокЗначений[0].Значение;
	Элементы.Список.ОбновитьТекстРедактирования();
	ПодключитьОбработчикОжидания("СписокОбновитьТекстРедактированияОбработчикОжидания", 0.1, Истина);
	
	ОбновитьТекстыОграниченияСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбновитьТекстРедактированияОбработчикОжидания()
	
	Элементы.Список.ОбновитьТекстРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУказанноеОграничение(УчитыватьЗависимости)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УчитыватьЗависимости", УчитыватьЗависимости);
	ДополнительныеПараметры.Вставить("Текст", СвойстваВидовПользователей[0].ТекстОграничения);
	ДополнительныеПараметры.Вставить("ТекстДляВнешнихПользователей",
		СвойстваВидовПользователей[1].ТекстОграничения);
	
	ОбновитьТекстыОграниченияСписка(ДополнительныеПараметры);
	
	Если Элементы.ОграничениеДоступа.ТекущаяСтраница = Элементы.ДляПользователей Тогда
		Если ЗначениеЗаполнено(СвойстваВидовПользователей[0].ТекстОшибокОграничения) Тогда
			Элементы.ОграничениеДоступа.ТекущаяСтраница = Элементы.ДляПользователейОшибки;
			ТекущийЭлемент = Элементы.ТекстОшибокОграничения;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(СвойстваВидовПользователей[1].ТекстОшибокОграничения) Тогда
			Элементы.ОграничениеДоступа.ТекущаяСтраница = Элементы.ДляВнешнихПользователейОшибки;
			ТекущийЭлемент = Элементы.ТекстОшибокОграниченияДляВнешнихПользователей;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстыОграниченияСписка(ДополнительныеПараметры = Неопределено)
	
	СвойстваСписка = СвойстваСписка(Список, ДополнительныеПараметры);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СвойстваСписка);
	
	Элементы.ОграничениеДоступа.Доступность = СвойстваСписка.СписокНайден;
	
	ОбновитьТекстыОграниченияСпискаДляВидаПользователей(СвойстваСписка.ДляПользователей, Ложь, ДополнительныеПараметры);
	ОбновитьТекстыОграниченияСпискаДляВидаПользователей(СвойстваСписка.ДляВнешнихПользователей, Истина, ДополнительныеПараметры);
	
	ЕстьОшибки = ЗначениеЗаполнено(СвойстваСписка.ДляПользователей.ТекстОшибокОграничения)
		Или ЗначениеЗаполнено(СвойстваСписка.ДляВнешнихПользователей.ТекстОшибокОграничения);
	
	Элементы.РассчитатьОграничениеВРоли.Доступность = Не ЕстьОшибки;
	Элементы.РассчитатьОграничениеВРолиДляВнешнихПользователей.Доступность = Не ЕстьОшибки;
	
	ОбновитьДоступностьТекстаДляВставки(ЕстьОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстыОграниченияСпискаДляВидаПользователей(Свойства, ДляВнешнихПользователей, ДополнительныеПараметры)
	
	Окончание = ?(ДляВнешнихПользователей, "ДляВнешнихПользователей", "");
	
	Элементы["ОграничениеВРолиЗаголовки" + Окончание].ТекущаяСтраница =
		?(Свойства.ОграничениеПоВладельцуВозможно = Истина,
			Элементы["ОграничениеВРолиЗаголовокСРасчетом" + Окончание],
			Элементы["ОграничениеВРолиЗаголовокБезРасчета" + Окончание]);
	
	ХранимыеСвойства = СвойстваВидовПользователей[?(ДляВнешнихПользователей, 1, 0)];
	ЗаполнитьЗначенияСвойств(ХранимыеСвойства, Свойства);
	
	Элементы[?(ДляВнешнихПользователей, "ДляВнешнихПользователейОшибки", "ДляПользователейОшибки")].Видимость =
		ЗначениеЗаполнено(ХранимыеСвойства.ТекстОшибокОграничения);
	
	Элементы["ТекстОграничения" + Окончание].ОбновитьТекстРедактирования();
	Элементы["ПроверитьТекстОграничения" + Окончание].Шрифт =
		Элементы["ПоказатьТекстДляВставки" + Окончание].Шрифт;
	
	Если ЗначениеЗаполнено(Свойства.ТекстОшибокОграничения)
	 Или Свойства.ОграничениеПоВладельцуВозможно = Истина
	   И (ДополнительныеПараметры = Неопределено
	      Или Не ДополнительныеПараметры.УчитыватьЗависимости) Тогда
		
		ХранимыеСвойства.ТекстОграниченияВРоли = НСтр("ru = '<Ограничение для роли не рассчитано>'");
		Элементы["РассчитатьОграничениеВРоли" + Окончание].Шрифт =
			Новый Шрифт(Элементы["ПоказатьТекстДляВставки" + Окончание].Шрифт,,, Истина);
	Иначе
		Элементы["РассчитатьОграничениеВРоли" + Окончание].Шрифт =
			Элементы["ПоказатьТекстДляВставки" + Окончание].Шрифт;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТекстаОграничения(ДляВнешнихПользователей)
	
	Окончание = ?(ДляВнешнихПользователей, "ДляВнешнихПользователей", "");
	
	Элементы["ПроверитьТекстОграничения" + Окончание].Шрифт =
		Новый Шрифт(Элементы["ПоказатьТекстДляВставки" + Окончание].Шрифт,,, Истина);
	
	Элементы["РассчитатьОграничениеВРоли" + Окончание].Шрифт =
		Новый Шрифт(Элементы["ПоказатьТекстДляВставки" + Окончание].Шрифт,,, Истина);
	
	Элементы.РассчитатьОграничениеВРоли.Доступность = Ложь;
	Элементы.РассчитатьОграничениеВРолиДляВнешнихПользователей.Доступность = Ложь;
	
	ХранимыеСвойства = СвойстваВидовПользователей[?(ДляВнешнихПользователей, 1, 0)];
	ХранимыеСвойства.ТекстОграниченияВРоли = НСтр("ru = '<Ограничение для роли не рассчитано>'");
	
	ОбновитьДоступностьТекстаДляВставки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьТекстаДляВставки(ЕстьОшибки)
	
	ТекстДляВставкиДоступен = Не ЕстьОшибки
		И (    ЗначениеЗаполнено(СвойстваВидовПользователей[0].ТекстОграничения)
		   Или ЗначениеЗаполнено(СвойстваВидовПользователей[1].ТекстОграничения) );
	
	Элементы.ПоказатьТекстДляВставки.Доступность = ТекстДляВставкиДоступен;
	Элементы.ПоказатьТекстДляВставкиДляВнешнихПользователей.Доступность = ТекстДляВставкиДоступен;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СвойстваСписка(Список, ДополнительныеПараметры = Неопределено)
	
	Свойства = Новый Структура;
	Свойства.Вставить("СписокНайден", Ложь);
	Свойства.Вставить("СписокПояснение", "");
	Свойства.Вставить("ТекстВМодулеМенеджера", Неопределено);
	Свойства.Вставить("СписокПолноеИмяКоллекции", "");
	Свойства.Вставить("НастройкиВнедрения",      Новый Структура);
	Свойства.Вставить("ДляПользователей",        СвойстваДляВидаПользователей());
	Свойства.Вставить("ДляВнешнихПользователей", СвойстваДляВидаПользователей());
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Список);
	
	Если ОбъектМетаданных = Неопределено Тогда
		Если ЗначениеЗаполнено(Список) Тогда
			Свойства.СписокПояснение = НСтр("ru = 'Список не найден'");
		Иначе
			Свойства.СписокПояснение = НСтр("ru = 'Список не выбран'");
		КонецЕсли;
		Возврат Свойства;
	КонецЕсли;
	
	Свойства.СписокНайден = Истина;
	
	Свойства.СписокПолноеИмяКоллекции =
		ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(ОбъектМетаданных) + "." + ОбъектМетаданных.Имя;
	
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	Попытка
		Результат = УправлениеДоступомСлужебный.РезультатПроверкиОграниченияДоступа(ПолноеИмя, ДополнительныеПараметры);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Результат = Неопределено;
	КонецПопытки;
	
	Если Результат = Неопределено Тогда
		Свойства.СписокПояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Список найден. Не удалось определить наличие ограничения по причине:
			           |%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		Возврат Свойства;
	КонецЕсли;
	
	Свойства.ТекстВМодулеМенеджера = Результат.ТекстВМодулеМенеджера;
	
	Если Результат.ТекстВМодулеМенеджера = Неопределено Тогда
		Свойства.СписокПояснение =
			НСтр("ru = 'Список найден. Не указан в процедуре ПриЗаполненииСписковСОграничениемДоступа общего модуля УправлениеДоступомПереопределяемый.'");
	Иначе
		Если Результат.ТекстВМодулеМенеджера Тогда
			Свойства.СписокПояснение = НСтр("ru = 'Список найден. Ограничение в модуле менеджера.'");
		Иначе
			Свойства.СписокПояснение = НСтр("ru = 'Список найден. Ограничение в переопределяемом модуле.'");
		КонецЕсли;
	КонецЕсли;
	
	Свойства.НастройкиВнедрения = Результат.НастройкиВнедрения;
	
	ЗаполнитьСвойстваСпискаДляВидаПользователей(Свойства.ДляПользователей,
		Результат.ДляПользователей, ДополнительныеПараметры);
	
	ЗаполнитьСвойстваСпискаДляВидаПользователей(Свойства.ДляВнешнихПользователей,
		Результат.ДляВнешнихПользователей, ДополнительныеПараметры);
	
	Возврат Свойства;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойстваДляВидаПользователей()
	
	Свойства = Новый Структура;
	
	Свойства.Вставить("ОграничениеПоВладельцуВозможно");
	Свойства.Вставить("ТекстОшибокОграничения");
	Свойства.Вставить("ТекстОграниченияВРоли");
	
	Возврат Свойства;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьСвойстваСпискаДляВидаПользователей(Свойства, Результат, ДополнительныеПараметры)
	
	Свойства.ОграничениеПоВладельцуВозможно = Результат.ОграничениеПоВладельцуВозможно;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Свойства.Вставить("ТекстОграничения", Результат.ПроверяемоеОграничение);
	КонецЕсли;
	
	ТекстОшибок = "";
	ОписаниеОшибок = Результат.ОписаниеОшибок;
	
	Если ЗначениеЗаполнено(ОписаниеОшибок) Тогда
		ТекстОшибок = ОписаниеОшибок.ТекстОшибок;
		ТекстОшибок = ТекстОшибок + Символы.ПС + Символы.ПС + ОписаниеОшибок.Ограничение;
		Если ЗначениеЗаполнено(ОписаниеОшибок.Дополнение) Тогда
			ТекстОшибок = ТекстОшибок + Символы.ПС + Символы.ПС + ОписаниеОшибок.Дополнение;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ОшибкаФормированияПараметровОграничения) Тогда
		ТекстОшибок = ТекстОшибок + Символы.ПС + Символы.ПС
			+ Результат.ОшибкаФормированияПараметровОграничения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ОшибкаФормированияТекстовЗапросов) Тогда
		ТекстОшибок = ТекстОшибок + Символы.ПС + Символы.ПС
			+ Результат.ОшибкаФормированияТекстовЗапросов;
	КонецЕсли;
	
	Свойства.ТекстОшибокОграничения = СокрЛП(ТекстОшибок);
	
	Если ЗначениеЗаполнено(Свойства.ТекстОшибокОграничения)
	 Или Не ЗначениеЗаполнено(Результат.ПроверяемоеОграничение)
	 Или Результат.ОграничениеВРолях = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ОграничениеВРолях.ШаблонДляОбъекта Тогда
		ТекстОграниченияВРоли = "#ДляОбъекта(";
		КоличествоПараметров = 1;
	Иначе
		ТекстОграниченияВРоли = "#ДляРегистра(";
		КоличествоПараметров = 6;
	КонецЕсли;
	ТекстОграниченияВРоли = ?(Результат.ОграничениеВРолях.ШаблонДляОбъекта, "#ДляОбъекта(", "#ДляРегистра(");
	
	ПервыйПараметр = Истина;
	Для Каждого Параметр Из Результат.ОграничениеВРолях.Параметры Цикл
		ТекстОграниченияВРоли = ТекстОграниченияВРоли + ?(ПервыйПараметр, "", ", ") + """" + Параметр + """";
		ПервыйПараметр = Ложь;
	КонецЦикла;
	
	Для НомерПараметра = Результат.ОграничениеВРолях.Параметры.Количество() + 1 По КоличествоПараметров Цикл
		ТекстОграниченияВРоли = ТекстОграниченияВРоли + ?(ПервыйПараметр, "", ", ") + """""";
		ПервыйПараметр = Ложь;
	КонецЦикла;
	
	Свойства.ТекстОграниченияВРоли = ТекстОграниченияВРоли + ")";
	
КонецПроцедуры

&НаКлиенте
Функция ТекстДляВставки()
	
	НомерПункта = 1;
	Текст = "";
	
	Если ТекстВМодулеМенеджера = Неопределено Тогда
		Текст =
			НСтр("ru = '1. В процедуру ПриЗаполненииСписковСОграничениемДоступа
			           |   общего модуля УправлениеДоступомПереопределяемый добавить строку:'");
		Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("
			           |
			           |	Списки.Вставить(Метаданные.%1, Истина);", СписокПолноеИмяКоллекции);
		Текст = Текст + Символы.ПС + Символы.ПС;
		НомерПункта = 2;
	КонецЕсли;
	
	Если ТекстВМодулеМенеджера <> Ложь Тогда
		Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. В модуле менеджера объекта метаданных %2 вставить или обновить процедуру:'"),
			НомерПункта,
			СписокПолноеИмяКоллекции);
			
		Текст = Текст + Символы.ПС + Символы.ПС +
		"#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		|#Область ПрограммныйИнтерфейс
		|#Область ДляВызоваИзДругихПодсистем
		|// СтандартныеПодсистемы.УправлениеДоступом
		|
		|// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
		|Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		|	" + ?(ЗначениеЗаполнено(СвойстваВидовПользователей[0].ТекстОграничения), "
		|	Ограничение.Текст =
		|	""" + ТекстСОтступом(СвойстваВидовПользователей[0].ТекстОграничения, "	|") + """;
		|	", "") + ?(ЗначениеЗаполнено(СвойстваВидовПользователей[1].ТекстОграничения), "
		|	Ограничение.ТекстДляВнешнихПользователей =
		|	""" + ТекстСОтступом(СвойстваВидовПользователей[1].ТекстОграничения, "	|") + """;
		|	", "") + "
		|КонецПроцедуры
		|
		|// Конец СтандартныеПодсистемы.УправлениеДоступом
		|#КонецОбласти
		|#КонецОбласти
		|#КонецЕсли";
	Иначе
		Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. В процедуре ПриЗаполненииОграниченияДоступа
			           |   общего модуля УправлениеДоступомПереопределяемый вставить или обновить строки:'"),
			НомерПункта,
			СписокПолноеИмяКоллекции);
			
		Текст = Текст + Символы.ПС + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"	Если Список = Метаданные.%1 Тогда
		|		
		|		Ограничение.Текст =
		|		""%2"";" + ?(ЗначениеЗаполнено(СвойстваВидовПользователей[1].ТекстОграничения), "
		|		
		|		Ограничение.ТекстДляВнешнихПользователей =
		|		""%3"";", "") + "
		|	
		|	КонецЕсли;",
		СписокПолноеИмяКоллекции,
		ТекстСОтступом(СвойстваВидовПользователей[0].ТекстОграничения, "		|"),
		ТекстСОтступом(СвойстваВидовПользователей[1].ТекстОграничения, "		|"));
	КонецЕсли;
	Текст = Текст + Символы.ПС + Символы.ПС;
	НомерПункта = НомерПункта + 1;
	
	Если ЗначениеЗаполнено(СвойстваВидовПользователей[0].ТекстОграничения)
	   И Не СтрНачинаетсяС(СвойстваВидовПользователей[0].ТекстОграничения, "<")
	 Или ЗначениеЗаполнено(СвойстваВидовПользователей[1].ТекстОграничения)
	   И Не СтрНачинаетсяС(СвойстваВидовПользователей[1].ТекстОграничения, "<") Тогда
	
		Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. В процедуре ПриЧтенииНаСервере формы элемента данных (если есть)
			           |   следует сделать вставку кода (для библиотек проверка подсистемы обязательна):'"),
			НомерПункта);
		
		Текст = Текст + Символы.ПС + Символы.ПС +
		"	// СтандартныеПодсистемы.УправлениеДоступом
		|	Если ОбщегоНазначения.ПодсистемаСуществует(""СтандартныеПодсистемы.УправлениеДоступом"") Тогда
		|		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль(""УправлениеДоступом"");
		|		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
		|	КонецЕсли;
		|	// Конец СтандартныеПодсистемы.УправлениеДоступом";
		
		НомерПункта = НомерПункта + 1;
		Текст = Текст + Символы.ПС + Символы.ПС;
	КонецЕсли;
	
	Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Запустить отчет ПроверкаВнедренияБСП.epf в режиме исправления с отбором по подсистеме
		           |   Управление доступом, чтобы проверить и обновить внедрение после изменения текста ограничения.'"),
		НомерПункта);
	Текст = Текст + Символы.ПС + Символы.ПС;
	Текст = Текст + "	" + НСтр("ru = 'Либо обновить внедрение вручную:'");
	
	ДобавитьОписаниеШаблонаРоли(Текст, СписокПолноеИмяКоллекции, СвойстваВидовПользователей[0],
		НСтр("ru = 'в роли с назначением для пользователей на права Чтение, Добавление, Изменение
		           |объекта метаданных %1 установить ограничение
		           |(и добавить соответствующий шаблон ограничения, если его еще нет в роли):'"));
	
	ДобавитьОписаниеШаблонаРоли(Текст, СписокПолноеИмяКоллекции, СвойстваВидовПользователей[1],
		НСтр("ru = 'в роли с назначением для внешних пользователей на права Чтение, Добавление, Изменение
		           |объекта метаданных %1 установить ограничение
		           |(и добавить соответствующий шаблон ограничения, если его еще нет в роли):'"));
	
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ВладелецЗначенийКлючейДоступа");
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ВладелецЗначенийКлючейДоступаОбъект");
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ВладелецЗначенийКлючейДоступаДокумент");
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ВладелецЗначенийКлючейДоступаНаборЗаписей");
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ВладелецЗначенийКлючейДоступаНаборЗаписейРегистраРасчета");
	ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, "ПолеРегистраКлючейДоступаКРегистрам");
	
	Если ЗначениеЗаполнено(НастройкиВнедрения.ТипыИзмеренийОтдельногоРегистраКлючей) Тогда
		ДобавитьОписаниеТребуемыхЭлементов(Текст,
			НастройкиВнедрения.ТипыИзмеренийОтдельногоРегистраКлючей.ИмяРегистраСведений,
			НастройкиВнедрения.ТипыИзмеренийОтдельногоРегистраКлючей.ТипыИзмерений,
			НСтр("ru = 'в регистр сведений %1 добавить типы:'"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиВнедрения.ПредопределенныйИдентификатор) Тогда
		ДобавитьОписаниеТребуемыхЭлементов(Текст,
			НастройкиВнедрения.ПредопределенныйИдентификатор.ИмяСправочника,
			НастройкиВнедрения.ПредопределенныйИдентификатор.ИмяПредопределенного,
			НСтр("ru = 'в справочник %1 добавить предопределенный элемент:'"));
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОписаниеШаблонаРоли(Текст, ПолноеИмя, ОписаниеШаблонаРоли, Описание)
	
	Если ЗначениеЗаполнено(ОписаниеШаблонаРоли.ТекстОграниченияВРоли) Тогда
		ШаблонРоли = ОписаниеШаблонаРоли.ТекстОграниченияВРоли;
	Иначе
		ШаблонРоли = НСтр("ru = '<Очистить ограничение, если указано и удалить шаблон, если не используется в роли>'");
	КонецЕсли;

	ДобавляемоеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, ПолноеИмя);
	ДобавляемоеОписание = "- " + ТекстСОтступом(ДобавляемоеОписание, "  ") + Символы.ПС + Символы.ПС;
	ДобавляемоеОписание = ДобавляемоеОписание + "  " + ТекстСОтступом(ШаблонРоли, "  ");
	
	Текст = Текст + Символы.ПС + "	" + ТекстСОтступом(ДобавляемоеОписание, "	") + Символы.ПС;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписаниеТребуемыхТипов(Текст, НастройкиВнедрения, ИмяОпределяемогоТипа)
	
	Если Не ЗначениеЗаполнено(НастройкиВнедрения[ИмяОпределяемогоТипа]) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьОписаниеТребуемыхЭлементов(Текст, ИмяОпределяемогоТипа, НастройкиВнедрения[ИмяОпределяемогоТипа],
		НСтр("ru = 'в определяемый тип %1 добавить типы:'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписаниеТребуемыхЭлементов(Текст, ПараметрЗаголовка, ОписаниеЭлементов, ЗаголовокОписания)
	
	ДобавляемоеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокОписания, ПараметрЗаголовка);
	ДобавляемоеОписание = "- " + ТекстСОтступом(ДобавляемоеОписание, "	") + Символы.ПС;
	ДобавляемоеОписание = ДобавляемоеОписание + "	" + ТекстСОтступом(ОписаниеЭлементов, "	");
	
	Текст = Текст + Символы.ПС + "	" + ТекстСОтступом(ДобавляемоеОписание, "	") + Символы.ПС;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСОтступом(Текст, Отступ)
	
	Возврат СтрЗаменить(Текст, Символы.ПС, Символы.ПС + Отступ);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭлементов(Контекст)
	
	Элементы = Контекст.Элементы;
	Элементы.ПапкаГотовойВыгрузкиКонфигурацииВФайлы.Доступность = Не Контекст.ВыполнятьВыгрузкуВоВременнуюПапку;
	
	Если Контекст.ИсточникОграничений = "ИзВыгрузкиКонфигурацииВФайлы" Тогда
		Элементы.ПолучениеОграничений.ТекущаяСтраница = Элементы.ОграниченияИзВыгрузки;
	Иначе
		Элементы.ПолучениеОграничений.ТекущаяСтраница = Элементы.ОграниченияИзСписка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаГотовойВыгрузкиКонфигурацииВФайлыНачалоВыбораПослеВыбора(ВыбранныеФайлы, Контекст) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		ПапкаГотовойВыгрузкиКонфигурацииВФайлы = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОписание(ПолноеОписание = Истина)
	
	СуществующееОписание = "";
	ЗаполнитьВидыОграниченийПравОбъектовМетаданных(СуществующееОписание);
	
	Если ИсточникОграничений <> "ИзВыгрузкиКонфигурацииВФайлы"
	   И ВсеОграниченияДоступа.ВысотаТаблицы = 0 Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Требуется заполнение табличного
			           |документа ""Все ограничения доступа"".
			           |Инструкции см. выше.'"));
		Возврат;
	КонецЕсли;
	
	НовоеОписание = НовоеОписаниеВидовОграниченийПрав();
	Если НовоеОписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолноеОписание Тогда
		Текст = Новый ТекстовыйДокумент;
		Текст.ДобавитьСтроку(
		"	Описание =
		|	""");
		
		Текст.ДобавитьСтроку(НовоеОписание);
		
		Текст.ДобавитьСтроку(
		"	|"";");
		
		Текст.Показать(НСтр("ru = 'Описание видов ограничений прав объектов метаданных'"));
		Возврат;
	КонецЕсли;
	
	НедостающиеСтроки = "";
	ЧислоСтрок = СтрЧислоСтрок(НовоеОписание);
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		Строка = СтрПолучитьСтроку(НовоеОписание, НомерСтроки);
		Если СтрНайти(СуществующееОписание, Сред(Строка, 3)) = 0 Тогда
			НедостающиеСтроки = НедостающиеСтроки + Строка + Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	
	ЛишниеСтроки = "";
	ЧислоСтрок = СтрЧислоСтрок(СуществующееОписание);
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		Строка = "	|" + СтрПолучитьСтроку(СуществующееОписание, НомерСтроки);
		Если СтрНайти(НовоеОписание, СокрП(Строка)) = 0 Тогда
			ЛишниеСтроки = ЛишниеСтроки + Строка + Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	
	Текст = Новый ТекстовыйДокумент;
	
	Текст.ДобавитьСтроку(НСтр("ru = 'Недостающие виды ограничений прав объектов метаданных:'"));
	Текст.ДобавитьСтроку(СокрП(НедостающиеСтроки));
	Текст.ДобавитьСтроку("");
	
	Текст.ДобавитьСтроку(НСтр("ru = 'Лишние виды ограничений прав объектов метаданных:'"));
	Текст.ДобавитьСтроку(СокрП(ЛишниеСтроки));
	Текст.ДобавитьСтроку("");
	
	Текст.Показать(НСтр("ru = 'Недостающие и лишние виды ограничений прав объектов метаданных'"));
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ЗаполнитьВидыОграниченийПравОбъектовМетаданных(СуществующееОписание)
	
	Список = УправлениеДоступомСлужебныйПовтИсп.ПостоянныеВидыОграниченийПравОбъектовМетаданных(Истина);
	Если ТипЗнч(Список) = Тип("Строка") Тогда
		СуществующееОписание = Список;
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Список Цикл
		СуществующееОписание = СуществующееОписание
			+ Строка.ПолноеИмяТаблицы + "." + Строка.Право + "." + Строка.ИмяВидаДоступа
			+ ?(ЗначениеЗаполнено(Строка.ПолноеИмяТаблицыОбъекта), "." + Строка.ПолноеИмяТаблицыОбъекта, "")
			+ Символы.ПС;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НовоеОписаниеВидовОграниченийПрав()
	
	ВидыОграниченийПрав.Очистить();
	
	ОшибкиВВыгрузке = "";
	ОпределитьВидыОграниченийПравНаСервере(ОшибкиВВыгрузке);
	Если ЗначениеЗаполнено(ОшибкиВВыгрузке) Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.ДобавитьСтроку(ОшибкиВВыгрузке);
		Если ИсточникОграничений = "ИзВыгрузкиКонфигурацииВФайлы" Тогда
			ЗаголовокДокумента = НСтр("ru = 'Ошибки при загрузке ограничений из файлов выгрузки конфигурации'");
		Иначе
			ЗаголовокДокумента = НСтр("ru = 'Ошибки при разборе ограничений, вставленных в табличный документ'");
		КонецЕсли;
		ТекстовыйДокумент.Показать(ЗаголовокДокумента);
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого Строка Из ВидыОграниченийПрав Цикл
		Если ВРег(Лев(Строка.Таблица, СтрДлина("Справочник."))) = ВРег("Справочник.") Тогда
			Строка.ПорядокКоллекции = 1;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("Документ."))) = ВРег("Документ.") Тогда
			Строка.ПорядокКоллекции = 2;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ЖурналДокументов."))) = ВРег("ЖурналДокументов.") Тогда
			Строка.ПорядокКоллекции = 3;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланВидовХарактеристик."))) = ВРег("ПланВидовХарактеристик.") Тогда
			Строка.ПорядокКоллекции = 4;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланСчетов."))) = ВРег("ПланСчетов.") Тогда
			Строка.ПорядокКоллекции = 5;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланВидовРасчета."))) = ВРег("ПланВидовРасчета.") Тогда
			Строка.ПорядокКоллекции = 6;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрСведений."))) = ВРег("РегистрСведений.") Тогда
			Строка.ПорядокКоллекции = 7;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрНакопления."))) = ВРег("РегистрНакопления.") Тогда
			Строка.ПорядокКоллекции = 8;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрБухгалтерии."))) = ВРег("РегистрБухгалтерии.") Тогда
			Строка.ПорядокКоллекции = 9;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрРасчета."))) = ВРег("РегистрРасчета.") Тогда
			Строка.ПорядокКоллекции = 10;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("БизнесПроцесс."))) = ВРег("БизнесПроцесс.") Тогда
			Строка.ПорядокКоллекции = 11;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("Задача."))) = ВРег("Задача.") Тогда
			Строка.ПорядокКоллекции = 12;
			
		КонецЕсли;
		
		Если Строка.Право = "Чтение" Тогда
			Строка.ПорядокПрав = 1;
		Иначе
			Строка.ПорядокПрав = 2;
		КонецЕсли;
	КонецЦикла;
	
	ВидыОграниченийПрав.Сортировать("ПорядокКоллекции Возр, Таблица Возр, ПорядокПрав Возр, ВидДоступа Возр, ТаблицаОбъекта Возр");
	
	НовоеОписание = "";
	
	Для каждого Строка Из ВидыОграниченийПрав Цикл
		
		НовоеОписание = НовоеОписание
			+ "	|"
			+ Строка.Таблица
			+ "." + Строка.Право
			+ "." + Строка.ВидДоступа
			+ ?(ЗначениеЗаполнено(Строка.ТаблицаОбъекта), "." + Строка.ТаблицаОбъекта, "")
			+ Символы.ПС;
		
	КонецЦикла;
	
	Возврат СокрП(НовоеОписание);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеНачальногоЗаполненияПрофилейНаВстроенномЯзыке(КонкретныйПрофиль)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Профили.Ссылка
	|ИЗ
	|	Справочник.ПрофилиГруппДоступа КАК Профили
	|ГДЕ
	|	Профили.Ссылка <> ЗНАЧЕНИЕ(Справочник.ПрофилиГруппДоступа.Администратор)
	|	И НЕ Профили.ПометкаУдаления
	|	И &ОтборЭлементов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Профили.Наименование";
	
	Если Метаданные.Справочники.ПрофилиГруппДоступа.Иерархический
	   И Метаданные.Справочники.ПрофилиГруппДоступа.ВидИерархии
	     = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
		
		УсловиеОтбораЭлементов = "(НЕ Профили.ЭтоГруппа)";
	Иначе
		УсловиеОтбораЭлементов = "Истина";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборЭлементов", УсловиеОтбораЭлементов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Текст = "";
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(КонкретныйПрофиль) И Выборка.Ссылка <> КонкретныйПрофиль Тогда
			Продолжить;
		КонецЕсли;
		Текст = Текст + ОписаниеНачальногоЗаполненияПрофиляНаВстроенномЯзыке(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Текст + Символы.ПС;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеНачальногоЗаполненияПрофиляНаВстроенномЯзыке(ПрофильСсылка)
	
	Профиль = ПрофильСсылка.ПолучитьОбъект();
	
	ИдентификаторПоставляемыхДанных = Строка(Профиль.ИдентификаторПоставляемыхДанных);
	
	ОписанияПрофилей = УправлениеДоступомСлужебныйПовтИсп.ОписаниеПоставляемыхПрофилей().ОписанияПрофилей;
	СвойстваПрофиля = ОписанияПрофилей.Получить(ИдентификаторПоставляемыхДанных);
	
	ОписаниеПоставляемогоПрофиля = "";
	Если СвойстваПрофиля <> Неопределено Тогда
		ОписаниеПоставляемогоПрофиля = СвойстваПрофиля.Описание;
	КонецЕсли;
	
	Описание = "";
	Для НомерСтроки = 1 По СтрЧислоСтрок(ОписаниеПоставляемогоПрофиля) Цикл
		Если ЗначениеЗаполнено(Описание) Тогда
			Описание = Описание
			+ "
			  |		           |";
		КонецЕсли;
		Описание = Описание + СтрПолучитьСтроку(ОписаниеПоставляемогоПрофиля, НомерСтроки);
	КонецЦикла;
	
	Имя = Профиль.ИмяПредопределенныхДанных;
	
	Если Не ЗначениеЗаполнено(Имя)
	   И СвойстваПрофиля <> Неопределено
	   И ЗначениеЗаполнено(СвойстваПрофиля.Имя) Тогда
		
		Имя = СвойстваПрофиля.Имя;
	КонецЕсли;
	
	Если Профиль.ИдентификаторПоставляемыхДанных
		= Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000") Тогда
		
		Идентификатор = Строка(Профиль.Ссылка.УникальныйИдентификатор());
	Иначе
		Идентификатор = Строка(Профиль.ИдентификаторПоставляемыхДанных);
	КонецЕсли;
	
	Текст = "
	|	// Профиль """ + Профиль.Наименование + """.
	|	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();" + ?(НЕ ЗначениеЗаполнено(Имя), "", "
	|	ОписаниеПрофиля.Имя           = """ + Имя + """") + ";
	|	ОписаниеПрофиля.Идентификатор = """ + Профиль.Ссылка.УникальныйИдентификатор() + """;
	|	ОписаниеПрофиля.Наименование  =
	|		НСтр(""ru = '" + Профиль.Наименование + "'"",
	|			Метаданные.ОсновнойЯзык.КодЯзыка);";
	
	Назначение = Новый Массив;
	Для Каждого Строка Из Профиль.Назначение Цикл
		Тип = ТипЗнч(Строка.ТипПользователей);
		Если Не ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
			Продолжить;
		КонецЕсли;
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Строка.ТипПользователей);
		ПустаяСсылкаОбъекта = МенеджерОбъекта.ПустаяСсылка();
		Если Назначение.Найти(ПустаяСсылкаОбъекта) = Неопределено Тогда
			Назначение.Добавить(ПустаяСсылкаОбъекта);
		КонецЕсли;
	КонецЦикла;
	
	Если Назначение.Количество() > 1
	 Или Назначение.Найти(Справочники.Пользователи.ПустаяСсылка()) = Неопределено Тогда
		
		Текст = Текст + "
		|	// Переопределение назначения.";
		Для Каждого ТекущееНазначение Из Назначение Цикл
			ИмяТипаСсылки = ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗнч(ТекущееНазначение));
			Текст = Текст + "
			|	ОписаниеПрофиля.Назначение.Добавить(Тип(""" + ИмяТипаСсылки + """));";
		КонецЦикла;
	КонецЕсли;
	
	Текст = Текст + "
	|	ОписаниеПрофиля.Описание =
	|		НСтр(""ru = '" + Описание + "'"");";

	
	ОписаниеРолей = Новый СписокЗначений;
	Для каждого ОписаниеРоли Из Профиль.Роли Цикл
		ОписаниеРолей.Добавить(ОписаниеРоли.Роль.Имя);
	КонецЦикла;
	ОписаниеРолей.СортироватьПоЗначению();
	
	Для каждого ОписаниеРоли Из ОписаниеРолей Цикл
		Текст = Текст + "
		|	ОписаниеПрофиля.Роли.Добавить(""" + ОписаниеРоли.Значение + """);"
	КонецЦикла;
	
	Для каждого ОписаниеВидаДоступа Из Профиль.ВидыДоступа Цикл
		
		ИмяВидаДоступа = УправлениеДоступомСлужебный.СвойстваВидаДоступа(ОписаниеВидаДоступа.ВидДоступа).Имя;
		
		Текст = Текст + "
		|	ОписаниеПрофиля.ВидыДоступа.Добавить(""" + ИмяВидаДоступа + """"
			+ ?(ОписаниеВидаДоступа.Предустановленный,
			    ", ""Предустановленный""",
			    ?(ОписаниеВидаДоступа.ВсеРазрешены,
			      ", ""ВначалеВсеРазрешены""", ""))
			+ ");";
		
		ОписаниеЗначенийДоступа = Профиль.ЗначенияДоступа.НайтиСтроки(Новый Структура("ВидДоступа", ОписаниеВидаДоступа.ВидДоступа));
		Для Каждого ОписаниеЗначенияДоступа Из ОписаниеЗначенийДоступа Цикл
			Если НЕ ЗначениеЗаполнено(ОписаниеЗначенияДоступа.ЗначениеДоступа) Тогда
				Продолжить;
			КонецЕсли;
			МетаданныеЗначения = ОписаниеЗначенияДоступа.ЗначениеДоступа.Метаданные();
			Если Метаданные.Перечисления.Найти(МетаданныеЗначения.Имя) = МетаданныеЗначения Тогда
				ИмяЗначенияДоступа = ОбщегоНазначения.ИмяЗначенияПеречисления(ОписаниеЗначенияДоступа.ЗначениеДоступа);
			Иначе
				ИмяЗначенияДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					ОписаниеЗначенияДоступа.ЗначениеДоступа, "ИмяПредопределенныхДанных");
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ИмяЗначенияДоступа) Тогда
				Продолжить;
			КонецЕсли;
			ИмяТаблицыЗначения = ОбщегоНазначения.ИмяТаблицыПоСсылке(ОписаниеЗначенияДоступа.ЗначениеДоступа);
			ПолноеИмяЗначенияДоступа = ИмяТаблицыЗначения + "." + ИмяЗначенияДоступа;
			Текст = Текст + "
			|	ОписаниеПрофиля.ЗначенияДоступа.Добавить(""" + ИмяВидаДоступа + """,
			|		""" + ПолноеИмяЗначенияДоступа + """);"
		КонецЦикла;
	КонецЦикла;
	
	Текст = Текст + "
	|	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	|";
	
	Возврат Текст;
	
КонецФункции

&НаСервере
Процедура ОпределитьВидыОграниченийПравНаСервере(ОшибкиВВыгрузке)
	
	ОграниченияДоступа = ОграниченияДоступа(ОшибкиВВыгрузке);
	Если ЗначениеЗаполнено(ОшибкиВВыгрузке) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ОграниченияДоступа",  ОграниченияДоступа);
	ПараметрыПроцедуры.Вставить("ВидыОграниченийПрав", ВидыОграниченийПрав);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ОпределитьВидыОграниченийПрав(ПараметрыПроцедуры);
	
КонецПроцедуры

// Возвращает таблицу с полями Таблица, Роль, Право, Поля, Ограничение,
// как вывод списка конфигуратора "Все ограничения доступа" в табличный документ.
//
// Параметры:
//  ПапкаВыгрузки - Строка - каталог, содержащий выгрузку конфигурации в файлы.
//                    Если не пустая строка, тогда выгрузка будет выполнена во временный каталог.
//
&НаСервере
Функция ОграниченияДоступа(ОшибкиВВыгрузке)
	
	Если ИсточникОграничений = "ИзВыгрузкиКонфигурацииВФайлы" Тогда
		ПапкаВыгрузки = ?(ВыполнятьВыгрузкуВоВременнуюПапку, "", ПапкаГотовойВыгрузкиКонфигурацииВФайлы);
		Возврат РеквизитФормыВЗначение("Объект").ОграниченияДоступаИзВыгрузкиКонфигурацииВФайлы(ПапкаВыгрузки, ОшибкиВВыгрузке);
	Иначе
		Возврат ОграниченияДоступаИзТабличногоДокумента(ОшибкиВВыгрузке);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОграниченияДоступаИзТабличногоДокумента(ОшибкиВВыгрузке)
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Таблица",     Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Роль",        Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Право",       Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Поля",        Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Ограничение", Новый ОписаниеТипов("Строка"));
	
	Права = Новый Соответствие;
	Права.Вставить(ВРег("Чтение"),     Истина);
	Права.Вставить(ВРег("Изменение"),  Истина);
	Права.Вставить(ВРег("Добавление"), Истина);
	Права.Вставить(ВРег("Удаление"),   Истина);
	
	Для НомерСтроки = 2 По ВсеОграниченияДоступа.ВысотаТаблицы Цикл
		
		Свойства = Новый Структура("Таблица, Роль, Право, Поля, Ограничение");
		Свойства.Таблица     = ВсеОграниченияДоступа.Область("R" + Формат(НомерСтроки,"ЧГ=") + "C1").Текст;
		Свойства.Роль        = ВсеОграниченияДоступа.Область("R" + Формат(НомерСтроки,"ЧГ=") + "C2").Текст;
		Свойства.Право       = ВсеОграниченияДоступа.Область("R" + Формат(НомерСтроки,"ЧГ=") + "C3").Текст;
		Свойства.Поля        = ВсеОграниченияДоступа.Область("R" + Формат(НомерСтроки,"ЧГ=") + "C4").Текст;
		Свойства.Ограничение = ВсеОграниченияДоступа.Область("R" + Формат(НомерСтроки,"ЧГ=") + "C5").Текст;
		
		Если Не ЗначениеЗаполнено(Свойства.Таблица)
		   И Не ЗначениеЗаполнено(Свойства.Роль)
		   И Не ЗначениеЗаполнено(Свойства.Право)
		   И Не ЗначениеЗаполнено(Свойства.Поля)
		   И Не ЗначениеЗаполнено(Свойства.Ограничение) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ЕстьОшибка = Ложь;
		
		МетаданныеТаблицы = Метаданные.НайтиПоПолномуИмени(Свойства.Таблица);
		МетаданныеРоли    = Метаданные.Роли.Найти(Свойства.Роль);
		
		Если МетаданныеТаблицы = Неопределено Тогда
			ДобавитьОшибку(ОшибкиВВыгрузке, НомерСтроки, ЕстьОшибка, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти объект метаданных ""%1"".'"), Свойства.Таблица));
		КонецЕсли;
		
		Если МетаданныеРоли = Неопределено Тогда
			ДобавитьОшибку(ОшибкиВВыгрузке, НомерСтроки, ЕстьОшибка, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти роль ""%1"".'"), Свойства.Роль));
		КонецЕсли;
		
		Если Права.Получить(ВРег(Свойства.Право)) = Неопределено Тогда
			ДобавитьОшибку(ОшибкиВВыгрузке, НомерСтроки, ЕстьОшибка, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное имя права ""%1"".'"), Свойства.Право));
		КонецЕсли;
		
		Если ЕстьОшибка Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПравоДоступа(Свойства.Право, МетаданныеТаблицы, МетаданныеРоли) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Свойства);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

&НаСервере
Процедура ДобавитьОшибку(ОшибкиВВыгрузке, НомерСтроки, ЕстьОшибка, ОписаниеОшибки)
	
	ЕстьОшибка = Истина;
	
	ОшибкиВВыгрузке = ОшибкиВВыгрузке  + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'В строке %1 табличного документа обнаружена ошибка:
		           |%2'"), Формат(НомерСтроки, "ЧГ="), ОписаниеОшибки) + Символы.ПС;
	
КонецПроцедуры

#КонецОбласти

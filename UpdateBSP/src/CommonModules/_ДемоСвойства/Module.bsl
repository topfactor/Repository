#Область СлужебныеПроцедурыИФункции

// Отключает использование наборов свойств внешних пользователей.
//
Процедура _ДемоОбновитьИспользованиеВнешнихПользователейПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = Источник.Значение;
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ВнешниеПользователи", ПараметрыНабора);
	
КонецПроцедуры

// См. УправлениеСвойствамиПереопределяемый.ЗаполнитьНаборыСвойствОбъекта.
//
Процедура ЗаполнитьНаборыСвойствОбъекта(Объект, ТипСсылки, НаборыСвойств, СтандартнаяОбработка, КлючНазначения) Экспорт
	
	Если ТипСсылки = Тип("СправочникСсылка._ДемоПартнеры") Тогда
		ЗаполнитьНаборыСвойствПартнера(Объект, ТипСсылки, НаборыСвойств);
		
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка._ДемоНоменклатура") Тогда
		ЗаполнитьНаборСвойствПоВидуНоменклатуры(Объект, ТипСсылки, НаборыСвойств);
		
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка._ДемоКонтрагенты") Тогда
		
		ЗаполнитьНаборСвойствКонтрагентов(Объект, ТипСсылки, НаборыСвойств);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции.

// Получает наборы свойств партнера по установленным флагам.
Процедура ЗаполнитьНаборыСвойствПартнера(Объект, ТипСсылки, НаборыСвойств)
	
	Если ТипЗнч(Объект) = ТипСсылки Тогда
		Партнер = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект, "Клиент, Конкурент, Поставщик, ПрочиеОтношения, ЭтоГруппа");
	Иначе
		Партнер = Объект;
	КонецЕсли;
	
	Если Партнер.ЭтоГруппа = Ложь Тогда
		
		Строка = НаборыСвойств.Добавить();
		Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Общие;
		Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
		Строка.ОтображатьЗаголовок = Истина;
		Строка.ОбщийНабор = Истина;
		Строка.Заголовок  = НСтр("ru = 'Для всех партнеров'");
		
		Если Партнер.Клиент Тогда
			Строка = НаборыСвойств.Добавить();
			Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Клиенты;
			Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			Строка.Заголовок = НСтр("ru = 'Для клиентов'");
			Строка.ОтображатьЗаголовок = Истина;
		КонецЕсли;
		
		Если Партнер.Конкурент Тогда
			Строка = НаборыСвойств.Добавить();
			Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Конкуренты;
			Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			Строка.ОтображатьЗаголовок = Истина;
			Строка.Заголовок = НСтр("ru = 'Для конкурентов'");
		КонецЕсли;
		
		Если Партнер.Поставщик Тогда
			Строка = НаборыСвойств.Добавить();
			Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Поставщики;
			Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			Строка.ОтображатьЗаголовок = Истина;
			Строка.Заголовок = НСтр("ru = 'Для поставщиков'");
		КонецЕсли;
		
		Если Партнер.ПрочиеОтношения Тогда
			Строка = НаборыСвойств.Добавить();
			Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры_Прочие;
			Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			Строка.ОтображатьЗаголовок = Истина;
			Строка.Заголовок = НСтр("ru = 'Для прочих'");
		КонецЕсли;
		
		// Свойства группы удаленных реквизитов.
		Строка = НаборыСвойств.Добавить();
		Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
		Строка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
		Строка.ОтображатьЗаголовок = Истина;
		Строка.Заголовок = НСтр("ru = 'Более не используемые реквизиты'");
	КонецЕсли;
	
КонецПроцедуры

// Получает набор свойств объекта по виду номенклатуры.
Процедура ЗаполнитьНаборСвойствПоВидуНоменклатуры(Объект, ТипСсылки, НаборыСвойств)
	
	Строка = НаборыСвойств.Добавить();
	Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник__ДемоНоменклатура_Общие;
	Строка.ОбщийНабор = Истина;
	
	Если ТипЗнч(Объект) = ТипСсылки Тогда
		Номенклатура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект, "ЭтоГруппа, ВидНоменклатуры");
	Иначе
		Номенклатура = Объект;
	КонецЕсли;
	
	Если Номенклатура.ЭтоГруппа = Ложь Тогда
		Строка = НаборыСвойств.Добавить();
		Строка.Набор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Номенклатура.ВидНоменклатуры, "НаборСвойств");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаборСвойствКонтрагентов(Объект, ТипСсылки, НаборыСвойств)
	
	Строка = НаборыСвойств.Добавить();
	Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ДемоКонтрагенты_Основное;
	
	Строка = НаборыСвойств.Добавить();
	Строка.Набор = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ДемоКонтрагенты_Прочее;
	
КонецПроцедуры

#КонецОбласти

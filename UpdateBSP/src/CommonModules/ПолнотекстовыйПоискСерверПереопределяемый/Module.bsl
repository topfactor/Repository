#Область ПрограммныйИнтерфейс

// Позволяет внести изменения в дерево с разделами полнотекстового поиска, отображаемого при выборе области поиска.
// По умолчанию дерево разделов формируется на основании состава подсистем конфигурации.
// Структура дерева описана в форме Обработка.ПолнотекстовыйПоискВДанных.Форма.ВыборОбластиПоиска.
// Все колонки, неуказанные в параметрах, будут автоматически рассчитаны.
// При необходимости построить дерево разделов самостоятельно требуется сохранить состав колонок.
//
// Параметры:
//   РазделыПоиска - ДеревоЗначений - области поиска. Содержит колонки:
//       ** Раздел   - Строка   - Представление раздела: подсистемы или объекта метаданных.
//       ** Картинка - Картинка - Картинка раздела, рекомендуется только для корневых разделов.
//       ** ОбъектМД - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Задается только для объектов метаданных,
//                     для разделов остается пустым.
// Пример:
//
//	НовыйРаздел = РазделыПоиска.Строки.Добавить();
//	НовыйРаздел.Раздел = "Главное";
//	НовыйРаздел.Картинка = БиблиотекаКартинок._ДемоРазделГлавное;
//	
//	ОбъектМетаданных = Метаданные.Документы._ДемоСчетНаОплатуПокупателю;
//	
//	Если ПравоДоступа("Просмотр", ОбъектМетаданных)
//		И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда 
//		
//		НовыйОбъектРаздела = НовыйРаздел.Строки.Добавить();
//		НовыйОбъектРаздела.Раздел = ОбъектМетаданных.ПредставлениеСписка;
//		НовыйОбъектРаздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
//	КонецЕсли;
//
Процедура ПриПолученииРазделовПолнотекстовогоПоиска(РазделыПоиска) Экспорт
	
	// _Демо начало примера
	
	// Добавление раздела "Главное".
	
	НовыйРаздел = РазделыПоиска.Строки.Добавить();
	НовыйРаздел.Раздел = "Главное";
	НовыйРаздел.Картинка = БиблиотекаКартинок._ДемоРазделГлавное;
	
	// Перед указанием объекта метаданных убедитесь, что его свойство ПолнотекстовыйПоиск
	// установлено в значение Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать.
	// Отдельную проверку для этого писать не нужно, т.к. режим поиска в режиме предприятия не меняется.
	ОбъектМетаданных = Метаданные.Документы._ДемоСчетНаОплатуПокупателю;
	
	Если ПравоДоступа("Просмотр", ОбъектМетаданных)
		И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда 
		
		НовыйОбъектРаздела = НовыйРаздел.Строки.Добавить();
		// Для формирования представления объекта рекомендуется использовать свойства объекта метаданных:
		// РасширенноеПредставлениеСписка, ПредставлениеСписка или метод Представление(), в зависимости от объекта.
		НовыйОбъектРаздела.Раздел = ОбъектМетаданных.ПредставлениеСписка;
		НовыйОбъектРаздела.ОбъектМД = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
	КонецЕсли;
	
	// Сортировка корневых разделов.
	
	ПорядокРазделов = Новый Массив;
	ПорядокРазделов.Добавить("Главное");
	ПорядокРазделов.Добавить("Управление доступом");
	ПорядокРазделов.Добавить("Синхронизация данных");
	ПорядокРазделов.Добавить("Сервисные подсистемы");
	ПорядокРазделов.Добавить("Интегрируемые подсистемы");
	ПорядокРазделов.Добавить("Интегрируемые подсистемы (часть 2)");
	ПорядокРазделов.Добавить("Бизнес-процессы и задачи");
	ПорядокРазделов.Добавить("Анкетирование");
	ПорядокРазделов.Добавить("Органайзер");
	ПорядокРазделов.Добавить("Справочники");
	ПорядокРазделов.Добавить("Инструменты разработчика");
	ПорядокРазделов.Добавить("Администрирование");
	
	КолонкаПорядка = РазделыПоиска.Колонки.Добавить("Порядок");
	
	Для Индекс = 0 По ПорядокРазделов.ВГраница() Цикл
		ИмяРаздела = ПорядокРазделов[Индекс];
		СтрокаРаздел = РазделыПоиска.Строки.Найти(ИмяРаздела, "Раздел");
		Если СтрокаРаздел <> Неопределено Тогда
			СтрокаРаздел.Порядок = Индекс;
		КонецЕсли;
	КонецЦикла;
	
	РазделыПоиска.Строки.Сортировать("Порядок");
	РазделыПоиска.Колонки.Удалить(КолонкаПорядка);
	
	// _Демо конец примера
	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайлами.ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, Параметры);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегламентПроектаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСФайламиКлиент.ОткрытьФормуВыбораФайлов(Объект.Ссылка, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентПроектаОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСФайламиКлиент.ОткрытьФормуФайла(Объект.РегламентПроекта, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаписатьПисьмо(Команда)
	
	ПараметрыСообщения = Новый Структура();
	Если ЗначениеЗаполнено(Объект.РегламентПроекта) Тогда
		ПараметрыСообщения.Вставить("РегламентПроекта", Объект.РегламентПроекта);
	КонецЕсли;
	ПараметрыСообщения.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыСообщения.Вставить("Проект", Объект.Ссылка);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиСообщения", ЭтотОбъект);
	ШаблоныСообщенийКлиент.СформироватьСообщение(Объект.Организация, "Письмо", Оповещение, Объект.Подразделение, ПараметрыСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеОтправкиСообщения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Истина Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не удалось отправить сформированное письмо. Подробнее см. в журнале регистрации.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти